GAL22V10	; Z2 AutoConfig, 64K, NoRomVec, ShutupValid, SingleBoard, AnySpace
ZComZ2 		; ManId: $2528 (9512), ProdId: $0000 (0000), SerialNr: $0000 (0000)

DOE /RESET RD /MATCH /BERR /CFGIN A2 A3 A4 A5 A6 GND
A1 D15 D14 D13 D12 /CFGOUT /SHUTUP /SLAVE /PRECFG /DBOE /BDSEL VCC

SHUTUP	= 	/RESET*MATCH*CFGIN*/RD*DOE*/CFGOUT*A6*/A5*/A4*A3*A2*/A1 +	; Any write at $4C
	  		/RESET*SHUTUP												; will shutup the board

PRECFG	= 	/RESET*SHUTUP +												; A write at $48 will
	  		/RESET*MATCH*CFGIN*/RD*DOE*/CFGOUT*A6*/A5*/A4*A3*/A2*/A1 +	; enable the address
	  		/RESET*PRECFG												; latch (D15-D8)

CFGOUT	= 	/RESET*/MATCH*PRECFG + 										; ConfigOut is enabled
	  		/RESET*CFGOUT												; after the write at $48

SLAVE 	= 	MATCH*CFGIN*/SHUTUP											; Slave is enabled at
																		; every matching address

BDSEL	= 	/RESET*MATCH*CFGIN*/BERR*/SHUTUP*CFGOUT						; Board is selected

DBOE	= 	/RESET*MATCH*CFGIN*/BERR*/SHUTUP*/RD +						; Data buffers enable
  			/RESET*MATCH*CFGIN*/BERR*/SHUTUP*RD*DOE						; line


; Data line 							Low @ address 

/D15.T	= 	/A6*/A5*/A4*/A3*/A2*A1 	+	; $02
	  		A6*/A5*/A4*/A3*/A2		+	; $40/42
			/A6*/A5*A4*/A3*A2*A1		; $16
	
/D14.T	= 	/A6*/A5*/A4*/A3*/A2*A1 	+	; $02
	  		A6*/A5*/A4*/A3*/A2 		+	; $40/42
			/A6*/A5*A4*/A3*/A2*A1		; $12

/D13.T	= 	/A6*/A5*/A4*/A3*/A2 	+	; $00/02
	  		A6*/A5*/A4*/A3*/A2 		+	; $40/42
			/A6*/A5*A4*/A3*/A2*/A1 	+	; $10
			/A6*/A5*A4*/A3*A2*/A1 		; $14

/D12.T	= 	/A6*/A5*/A4*/A3*/A2*/A1 +	; $00
	  		A6*/A5*/A4*/A3*/A2 		+	; $40/42
			/A6*/A5*A4*/A3*/A2*A1		; $12


D15.E	= /RESET*MATCH*CFGIN*/CFGOUT*RD*/BERR*/SHUTUP	; AutoConfig GAL Enable
D14.E	= /RESET*MATCH*CFGIN*/CFGOUT*RD*/BERR*/SHUTUP	; (i.e. AutoConfig data is
D13.E	= /RESET*MATCH*CFGIN*/CFGOUT*RD*/BERR*/SHUTUP	; sent out only when the board
D12.E	= /RESET*MATCH*CFGIN*/CFGOUT*RD*/BERR*/SHUTUP	; is not yet configured)

DESCRIPTION:

